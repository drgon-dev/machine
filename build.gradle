plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.machine.Main'
        )
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file("config/checkstyle/checkstyle.xml")
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.machine'
            artifactId = 'machine'
            version = '1.0.12'
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/drgon-dev/machine"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

group = 'org.machine'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'


    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.assertj:assertj-swing:3.17.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

test {
    systemProperty 'java.awt.headless', 'false'
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

check.dependsOn checkstyleMain, checkstyleTest
build.dependsOn check, test

tasks.register('generateOpenApiSpec', Test) {
    group = 'documentation'
    description = 'Generates the OpenAPI specification file.'
    useJUnitPlatform()
    include 'com/yourproject/OpenApiSpecGenerator.class'
    reports {
        junitXml.outputLocation = layout.buildDirectory.dir("reports/openapi-test")
        html.outputLocation = layout.buildDirectory.dir("reports/openapi-test-html")
    }
    doLast {
        copy {
            from 'build/openapi/'
            into 'build/api-spec/'
        }
    }
}